
buildscript {
    ext.cubaVersion = '7.0.2'
    repositories {
        maven {
            url 'https://repo.cuba-platform.com/content/groups/work'
            credentials {
                username(rootProject.hasProperty('repoUser') ? rootProject['repoUser'] : 'cuba')
                password(rootProject.hasProperty('repoPass') ? rootProject['repoPass'] : 'cuba123')
            }
        }
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
    }
}

def modulePrefix = 'app'

def globalModule = project(":${modulePrefix}-global" )
def coreModule = project(":${modulePrefix}-core" )
def webModule = project(":${modulePrefix}-web" )
def polymerClientModule = project(":${modulePrefix}-polymer-client" )

def servletApi = 'javax.servlet:javax.servlet-api:3.1.0'

apply(plugin: 'cuba')

cuba {
    artifact {
        group = 'akkount'
        version = '0.2'
        isSnapshot = true
    }
    tomcat {
        dir = "$project.rootDir/deploy/tomcat"
    }
    ide {
        
        vcs = 'Git'
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
    appComponent("com.haulmont.addon.globalevents:cubaglevt-global:0.3.0")
}

def hsql = 'org.hsqldb:hsqldb:2.4.1'

configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
        testCompile('junit:junit:4.12')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(globalModule) {
    entitiesEnhancing {
        main {
            enabled = true
        }
    }
}

configure(coreModule) {
    apply(plugin: 'groovy')

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        compileOnly(servletApi)
        jdbc(hsql)
        testRuntime(hsql)
        testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-2') {
            exclude(group: 'org.codehaus.groovy', module: 'groovy-all')
        }
    }

    sourceSets {
        test {
            groovy {
                srcDir 'testspec'
            }
        }
    }

    task cleanConf(description: 'Cleans up conf directory') << {
        def dir = new File(cuba.tomcat.dir, "/conf/${modulePrefix}-core")
        if (dir.isDirectory()) {
            ant.delete(includeemptydirs: true) {
                fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}-core"
        appJars(modulePrefix + '-global', modulePrefix + '-core')
    }

    task startDb(description: 'Starts HSQLDB', type: CubaHsqlStart) {
        dbName = 'akk'
    }

    task createDb(dependsOn: assemble, description: 'Creates local database', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'akk'
        dbUser = 'sa'
        dbPassword = ''
    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'akk'
        dbUser = 'sa'
        dbPassword = ''
    }

    task startTestDb(description: 'Starts HSQLDB for tests', type: CubaHsqlStart) {
        dbPort = 9002
        dbName = 'akk_test'
    }

    task createTestDb(dependsOn: assemble, description: 'Creates local test database', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost:9002'
        dbName = 'akk_test'
        dbUser = 'sa'
        dbPassword = ''
    }
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        compileOnly(servletApi)
        compile(globalModule)

    }
    
    task webArchive(type: Zip) {
        from file("$buildDir/web")
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deployConf(type: Copy) {
        from file('src')
        include "akkount/**"
        into "$cuba.tomcat.dir/conf/${modulePrefix}"
    }

    task clearMessagesCache(type: CubaClearMessagesCache) {
        appName = "${modulePrefix}"
    }
    deployConf.dependsOn clearMessagesCache

    task cleanConf(description: 'Cleans up conf directory') << {
        def dir = new File(cuba.tomcat.dir, '/conf/${modulePrefix}')
        if (dir.isDirectory()) {
            ant.delete(includeemptydirs: true) {
                fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}"
        appJars(modulePrefix + '-global', modulePrefix + '-web')
    }
    task buildScssThemes(type: CubaWebScssThemeCreation)
    task deployThemes(type: CubaDeployThemeTask, dependsOn: buildScssThemes)
    assemble.dependsOn buildScssThemes
}




configure(polymerClientModule) {
    apply(plugin: 'cuba')

    def frontAppDir = "${modulePrefix}-front"

    task installBowerPackages(type: NodeTask, dependsOn: npmInstall) {
        script = file("node_modules/bower/bin/bower")
        args = ['install','-F']
        inputs.file "bower.json"
        outputs.dir "bower_components"
    }

    task assemble(type: NodeTask, dependsOn: installBowerPackages) {
        script = file("node_modules/polymer-cli/bin/polymer")
        args = ['build']
        inputs.dir "./"
        outputs.dir "build"
    }

    task deployUnbundled(type: Copy) {
        from file('./')
        include 'bower_components/**'
        include 'src/**'
        into "$cuba.tomcat.dir/webapps/$frontAppDir"
    }

    task deploy(type: Copy, dependsOn: [assemble, deployUnbundled]) {
        from file('build/es6-unbundled')
        into "$cuba.tomcat.dir/webapps/$frontAppDir"
    }

    task servePolymer(type: NodeTask, dependsOn: assemble) {
        script = file("node_modules/gulp/bin/gulp.js")
        args = ['serve']
    }
}

task undeploy(type: Delete, dependsOn: ":${modulePrefix}-web:cleanConf" ) {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-core" )
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}" )
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-front" )
}

task restart(dependsOn: ['stop', ":${modulePrefix}-core:deploy" , ":${modulePrefix}-web:deploy" ], description: 'Redeploys applications and restarts local Tomcat') << {
    ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
        not {
            socket(server: 'localhost', port: '8787')
        }
    }
    start.execute()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1'
}

task buildWar(type: CubaWarBuilding) {
    webXmlPath = 'modules/web/web/WEB-INF/single-war-web.xml'
    coreContextXmlPath = 'modules/core/web/META-INF/prod-context.xml'
    appProperties = ['cuba.automaticDatabaseUpdate'       : 'true',
                     'cuba.web.loginDialogDefaultUser'    : '<disabled>',
                     'cuba.web.loginDialogDefaultPassword': '<disabled>',
                     'akk.shutdownDatabaseOnExit'         : 'true']
    includeContextXml = true
    includeJdbcDriver = true
    appHome = '${app.home}'
    logbackConfigurationFile = 'etc/war-logback.xml'
}